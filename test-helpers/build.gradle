plugins {
    id 'java'
}

task deploy(dependsOn: ["deployJarsForEmbulkTestMavenPlugin"])

/*
 * Following tasks are for building/deploying JARs for testing org.embulk.plugin.jar.TestJarPluginLoader
 */
task deployJarsForEmbulkTestMavenPlugin(
        type: Copy,
        dependsOn: ["buildFlatJarForEmbulkTestMavenPlugin", "buildNestedJarEmbulkTestMavenPlugin"]) {
    from("$buildDir/libs") {
        include "**/embulk-test-maven-plugin-*.jar"
    }
    into "$buildDir/embulk-test-maven-plugin"
}

task buildFlatJarForEmbulkTestMavenPlugin(type: Jar) {
    from(sourceSets.main.runtimeClasspath) {
        include "org/embulk/plugin/jar/ExampleJarSpiV0.class"
        include "embulk-test-maven-plugin/main.txt"
    }
    manifest {
        attributes(commonManifestForEmbulkTestMavenPlugin())
    }
    archiveName = "embulk-test-maven-plugin-flat.jar"
}

task buildNestedJarEmbulkTestMavenPlugin(type: Jar, dependsOn: ["buildDepsJarForEmbulkTestMavenPlugin"]) {
    from(sourceSets.main.runtimeClasspath) {
        include "org/embulk/plugin/jar/ExampleJarSpiV0.class"
        include "embulk-test-maven-plugin/main.txt"
    }
    into "classpath", { from "$buildDir/libs/embulk-test-maven-plugin-deps.jar" }
    manifest {
        attributes(commonManifestForEmbulkTestMavenPlugin() << [
                "Embulk-Plugin-Class-Path": "classpath/embulk-test-maven-plugin-deps.jar"
        ])
    }
    archiveName = "embulk-test-maven-plugin-nested.jar"
}

task buildDepsJarForEmbulkTestMavenPlugin(type: Jar) {
    from(sourceSets.main.runtimeClasspath) {
        include "org/embulk/plugin/jar/ExampleDependencyJar.class"
        include "embulk-test-maven-plugin/deps.txt"
    }
    archiveName = "embulk-test-maven-plugin-deps.jar"
}

static def commonManifestForEmbulkTestMavenPlugin() {[
        "Manifest-Version": "1.0",
        "Embulk-Plugin-Spi-Version": "0",
        "Embulk-Plugin-Main-Class": "org.embulk.plugin.jar.ExampleJarSpiV0"
]}
