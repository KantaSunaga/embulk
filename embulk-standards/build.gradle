// TODO: Have more details in description and summary.
description = 'Embulk standard plugins'
ext {
    summary = 'Embulk standard plugins'
}

// include ruby scripts to jar. don't use sourceSets.main.resources.srcDirs
// because IntelliJ causes error if srcDirs includes files out of projectDir.
processResources.from("${buildDir}/embulk_gem_complements")

repositories {
    mavenCentral()
}

dependencies {
    compile project(':embulk-core')
    compile 'org.apache.commons:commons-compress:1.10'

    testCompile 'junit:junit:4.12'
    testCompile project(':embulk-core').sourceSets.test.output
    testCompile project(':embulk-test')
    testCompile 'ch.qos.logback:logback-classic:1.1.3'

    // TODO: Remove this, and load it with the proper DependencyClassLoader.
    // This statement gets it loaded by the top-level ClassLoader.
    testCompile project(':embulk-deps-buffer')
    testCompile project(':embulk-deps-config')
    testCompile project(':embulk-deps-guess')
}

task rubyTestVanilla(type: JavaExec, dependsOn: [
        "jar",
        "prepareDependencyJars",
        "installDependencyGems",
        ":embulk-deps-buffer:prepareDependencyJars",
        ":embulk-deps-config:prepareDependencyJars",
        ":embulk-deps-guess:prepareDependencyJars",
        ]) {
    workingDir = file("${project.projectDir}");
    classpath = sourceSets.main.runtimeClasspath  // Not "configurations.runtimeClasspath" to use compiled embulk-core
    main = "org.jruby.Main"
    args = ["-Isrc/main/ruby", "-Isrc/test/ruby", "--debug", "./src/test/ruby/vanilla/run-test.rb"]
    environment "GEM_HOME": "${buildDir}/dependency_gems_installed"
}
test.dependsOn(['rubyTestVanilla'])

task installDependencyGems(type: JavaExec) {
    doFirst {
        delete("${buildDir}/dependency_gems_installed")
        mkdir("${buildDir}/dependency_gems_installed")
    }

    workingDir = file("${projectDir}")
    classpath = configurations.runtimeClasspath  // Not "sourceSets.main.runtimeClasspath" not to depend on "jar"
    main = "org.jruby.Main"
    args = ["-S", "gem", "install", "simplecov:0.10.0", "test-unit:3.0.9"]
    environment "GEM_HOME": "${buildDir}/dependency_gems_installed"
}

String buildRubyStyleVersionFromJavaStyleVersion(String javaStyleVersion) {
    if (javaStyleVersion.contains('-')) {
        List<String> versionTokens = javaStyleVersion.tokenize('-');
        if (versionTokens.size() != 2) {
            throw new GradleException('Invalid project version: ' + javaStyleVersion);
        }
        return versionTokens.get(0) + '.' + versionTokens.get(1).toLowerCase();
    }
    else {
        return javaStyleVersion;
    }
}

task prepareDependencyJars(type: Copy) {
    doFirst {
        delete("${buildDir}/dependency_jars")
    }
    from configurations.runtime
    into "${buildDir}/dependency_jars"
}

task complementEmbulkGem {
    doFirst {
        delete("${buildDir}/embulk_gem_complements")
    }
    doLast {
        def rubyVersion = buildRubyStyleVersionFromJavaStyleVersion("${project.version}")
        file("${buildDir}/embulk_gem_complements/gems/embulk-${rubyVersion}-java/lib").mkdirs()
        copy {
            from "${projectDir}/src/main/ruby"
            into "${buildDir}/embulk_gem_complements/gems/embulk-${rubyVersion}-java/lib"
        }
    }
}
processResources.dependsOn("complementEmbulkGem")
