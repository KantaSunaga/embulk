repositories {
    mavenCentral()
}

configurations {
    rdocRuntime
}

dependencies {
    rdocRuntime "org.jruby:jruby-complete:" + rootProject.jrubyVersion
}

task sphinxHtml(type: Exec) {
    workingDir '.'
    commandLine 'make'
    args 'html'
}

task javadocHtml(type: Copy, dependsOn: [':embulk-core:javadoc']) {
    doFirst { file('build/html/javadoc').mkdirs() }
    from project(':embulk-core').javadoc.destinationDir
    into 'build/html/javadoc'
}

task installGems(type: JavaExec) {
    doFirst {
        delete("${buildDir}/gems")
        mkdir("${buildDir}/gems")
    }

    workingDir = file("${projectDir}")
    classpath = configurations.rdocRuntime
    main = "org.jruby.Main"
    args = ["-S", "gem", "install", "yard:0.9.12"]
    environment "GEM_HOME": "${buildDir}/gems"
}

task rdocHtml(type: JavaExec, dependsOn: "installGems") {
    workingDir '..'
    classpath = configurations.rdocRuntime
    main = "org.jruby.Main"
    args = ["-ryard", "-eYARD::CLI::Yardoc.run('embulk-core/src/main/ruby', '-o', 'embulk-docs/build/html/rdoc')"]
    environment "GEM_HOME": "${buildDir}/gems"
}

task site(type: Copy, dependsOn: ['sphinxHtml', 'rdocHtml', 'javadocHtml']) {
}
